PREFIX clonto: <http://myCLOnto.de/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?categoryname 
?depth
(COUNT(?category2) as ?NrOfReachedCategories)
(MAX(?c2depth) as ?maxc2depth)
(AVG(?c2depth) as ?avgc2depth)
(MAX(?NrOfmediators) as ?maxdepthmediators)
(AVG(?NrOfmediators) as ?avgdepthmediators) 
 
WHERE {
	?category clonto:depth ?depth .
	?category clonto:name ?categoryname .
	
	{SELECT DISTINCT ?category ?category2 (xsd:integer(?category2depth) as ?c2depth) (COUNT(?category3) as ?NrOfmediators) 
     WHERE  {  
		?category clonto:hasSubCategory* ?category3 .
		?category3 clonto:hasSubCategory+ ?category2 .
		?category2 clonto:depth ?category2depth .
	}
    group by ?category ?category2 ?category2depth}
}
GROUP BY ?categoryname ?depth
ORDER BY ?depth ?maxc2depth ?maxdepthmediators