PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://myCLOnto.de/> 
SELECT ?ename ?catname ?t0name ?t1name ?howMany
WHERE {
    ?cat :hasEntity ?entity .
    ?cat :name ?catname .
    ?entity :name ?ename .
    
    {SELECT ?cat (COUNT(?e2) as ?howMany)
     WHERE {
       ?cat :hasEntity ?e2 .
     }
     GROUP BY ?cat
     HAVING (?howMany > 2)
    }
    
    ?cl :name "Computer languages" .
    ?cl :hasSubCategory ?t0 .
    ?t0 :name ?t0name .
    FILTER NOT EXISTS { ?t0 :hasSubCategory* ?cat . }
    ?cl :hasSubCategory ?t1 .
    ?t1 :name ?t1name .
    FILTER (?t0 != ?t1)
    
    FILTER EXISTS { ?t1 :hasSubCategory* ?cat . }
    
    FILTER EXISTS { 
        ?entity ^:hasEntity ?temp .
        ?t0 :hasSubCategory* ?temp . }
        
    FILTER NOT EXISTS { 
        ?cat :hasEntity ?e2 .
        FILTER(?e2 != ?entity)
        FILTER EXISTS {?t0 :hasSubCategory*/:hasEntity ?e2 . }
    }
}
ORDER BY ?howManyEntities ?ename