PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://myCLOnto.de/> 
SELECT DISTINCT ?ename ?distcatname ?t1name ?t2name
WHERE {
    ?distcat :hasEntity ?entity .
    
    ?cl :name "Computer languages" .
    ?cl :hasSubCategory ?t1 .
    ?cl :hasSubCategory ?t2 .
    
    FILTER NOT EXISTS{ ?t1 :hasSubCategory* ?t2 .
                       ?t1 :hasSubCategory+ ?distcat .}
    FILTER NOT EXISTS{ ?t2 :hasSubCategory* ?t1 .}
    ?t2 :hasSubCategory+ ?distcat.
    
    FILTER NOT EXISTS{
        ?cat :hasEntity ?entity .
        FILTER(?cat != ?distcat)
        ?t2 :hasSubCategory+ ?cat .
        FILTER NOT EXISTS{
            ?t1 :hasSubCategory+ ?cat .
        }
    }
    
    
    ?distcat :name ?distcatname .
    ?entity :name ?ename .
    ?t1 :name ?t1name .
    ?t2 :name ?t2name .
    FILTER(?t1name < ?t2name)
    
}
ORDER BY ?t2name ?distcatname